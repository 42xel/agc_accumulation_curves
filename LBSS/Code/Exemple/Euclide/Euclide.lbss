//examplifies the basic of labels, nodes linking and branching.
//usage
/*
python3 ../../lbss.py Euclide.lbss ./agc/
*/

create_lbss_machine << machine_euclide
//computes the gcd of x_0 and x_1 in x_0
	beginning:	//declaration of a label (ends with ':')
	a = 0;  	//not necessary, it's implicitely assumed.
	a += 1*h;
	>;		//move right
	labelTest:
	Branch labelSwap, labelEnd, labelNext;
	//goes to labelSwap, labelEnd, labelNext depending on whether y<0, y=0, 0<y respectively
	labelNext:
	a -= 1 * h; goto labelTest
	//a goto. Preferably writen on the same line or with indentation because, relative to either model (lbss or agc), it is an alteration of the natural linking of the "a-=h;" node/metasignal.
	//natural linking meaning, to (the node/metasignal corresponding to) the next line.
	// no semi colon! it is not a node! 
	labelSwap:
	a += 1*h;
	h -= 1*a;
	a += 1*h;
	<;
	h = 0;
	h += 1*a;
	a=0; goto beginning
	labelEnd:
	a += 1 *  h;
	h = 0;
	<;
	h = 0;
	h += 1*a;
	a = 0;
	end;
machine_euclide


create_lbss_configuration << configuration_euclide
x = ["33/5", "24/7"]
configuration_euclide

toAGC_machine machine_euclide machine_euclide.agc

toAGC_run machine_euclide.agc configuration_euclide run_euclide.agc run_euclide.pdf beginning 0 -1 0 1/10

run machine_euclide configuration_euclide run -1 beginning

print run


